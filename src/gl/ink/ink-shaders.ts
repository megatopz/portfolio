export const vertex = /* glsl */ `
attribute vec2 position;
attribute vec2 uv;
varying vec2 vUv;
void main() {
  vUv = uv;
  gl_Position = vec4(position, 0.0, 1.0);
}
`;

export const fragment = /* glsl */ `
precision highp float;
uniform float uProgress;
uniform float uDirection;
uniform float uSeed;
uniform vec3 uColor;
varying vec2 vUv;

float hash(vec2 p) {
  return fract(sin(dot(p, vec2(127.1, 311.7))) * 43758.5453);
}

float noise(vec2 p) {
  vec2 i = floor(p);
  vec2 f = fract(p);
  vec2 u = f * f * (3.0 - 2.0 * f);
  return mix(mix(hash(i), hash(i + vec2(1.0, 0.0)), u.x),
             mix(hash(i + vec2(0.0, 1.0)), hash(i + vec2(1.0, 1.0)), u.x), u.y);
}

float fbm(vec2 p) {
  float value = 0.0;
  float amplitude = 0.5;
  for (int i = 0; i < 5; i++) {
    value += amplitude * noise(p);
    p *= 2.0;
    amplitude *= 0.5;
  }
  return value;
}

void main() {
  float gradient = uDirection > 0.0 ? vUv.x : 1.0 - vUv.x;
  float field = gradient * 0.6 + fbm(vUv * 3.0 + uSeed) * 0.4;
  float edge = uProgress * 1.2 - 0.1;
  float alpha = 1.0 - smoothstep(edge - 0.04, edge + 0.04, field);
  gl_FragColor = vec4(uColor * alpha, alpha);
}
`;
